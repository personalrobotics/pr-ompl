###############################################################################
# CMake config file for gls
#
# This sets the following variables:
# @PROJECT_NAME@_INCLUDE_DIRS - Directories containing the gls include files.
# @PROJECT_NAME@_LIBRARIES    - Libraries needed to use gls.
# @PROJECT_NAME@_VERSION      - gls version.
###############################################################################

@PACKAGE_INIT@

function(gls_traverse_components output_variable)
  set(output_components)

  foreach(component ${ARGN})
    # We should be able to eliminate the <C>_VISITED property by detecting
    # whether the <C>_FOUND property is DEFINED. Unfortunately, DEFINED checks
    # whether or not define_property() was called, not whether the variable was
    # set. The exact behavior seems to vary between CMake versions, so we use
    # an extra variable instead.
    get_property(is_visited DIRECTORY PROPERTY "gls_${component}_VISITED")
    set_property(DIRECTORY PROPERTY "gls_${component}_VISITED" TRUE)

    if(NOT is_visited)
      set(component_include
        "${CMAKE_CURRENT_LIST_DIR}/gls_${component}Component.cmake")
      set(target_include
        "${CMAKE_CURRENT_LIST_DIR}/gls_${component}Targets.cmake")

      if(EXISTS "${component_include}" AND EXISTS "${target_include}")
        include("${component_include}")

        set_property(DIRECTORY PROPERTY "gls_${component}_FOUND" TRUE)
        set_property(DIRECTORY
          PROPERTY "gls_${component}_INCLUDE_DIRS"
          ${gls_${component}_INCLUDE_DIRS})
        set_property(DIRECTORY
          PROPERTY "gls_${component}_DEFINITIONS"
          ${gls_${component}_DEFINITIONS})
        set_property(DIRECTORY
          PROPERTY "gls_${component}_LIBRARIES"
          ${gls_${component}_LIBRARIES})
        set_property(DIRECTORY
          PROPERTY "gls_${component}_DEPENDENCIES"
          ${gls_${component}_DEPENDENCIES})

        gls_traverse_components("${output_variable}" 
          ${gls_${component}_DEPENDENCIES})

        include("${target_include}")

        # This is not necessary in recent versions of cmake, which supports
        # transitive includes via INTERFACE_INCLUDE_DIRECTORIES.
        foreach(dependency ${gls_${component}_DEPENDENCIES})
          set_property(DIRECTORY APPEND
            PROPERTY "gls_${component}_INCLUDE_DIRS"
            ${gls_${dependency}_INCLUDE_DIRS})
          set_property(DIRECTORY APPEND
            PROPERTY "gls_${component}_DEFINITIONS"
            ${gls_${dependency}_DEFINITIONS})
          # LIBRARIES is automatically inherited via INTERFACE_LINK_LIBRARIES.
        endforeach()

        list(APPEND output_components "${component}")
      else()
        set_property(DIRECTORY PROPERTY "gls_${component}_FOUND" FALSE)
      endif()
    endif()
  endforeach()

  set("${output_variable}" ${output_components} PARENT_SCOPE)
endfunction()

function(gls_export_variable variable)
  set("${variable}" ${${variable}} CACHE INTERNAL "")
endfunction()

function(gls_export_property variable)
  get_property(value DIRECTORY PROPERTY "${variable}")
  set("${variable}" ${value} CACHE INTERNAL "")
endfunction()

function(gls_export_notfound variable)
  set("${variable}" "${variable}-NOTFOUND" CACHE INTERNAL "")
endfunction()

function(gls_package_init)
  gls_traverse_components(required_components "${ARGN}")

  set(gls_INCLUDE_DIRS)
  set(gls_DEFINITIONS)
  set(gls_LIBRARIES)

  foreach(component ${ARGN})
    gls_export_property("gls_${component}_FOUND")

    if(gls_${component}_FOUND)
      gls_export_property("gls_${component}_INCLUDE_DIRS")
      gls_export_property("gls_${component}_DEFINITIONS")
      gls_export_property("gls_${component}_LIBRARIES")
      gls_export_property("gls_${component}_DEPENDENCIES")

      list(APPEND gls_INCLUDE_DIRS ${gls_${component}_INCLUDE_DIRS})
      list(APPEND gls_DEFINITIONS ${gls_${component}_DEFINITIONS})
      list(APPEND gls_LIBRARIES ${gls_${component}_LIBRARIES})
    else()
      gls_export_notfound("gls_${component}_INCLUDE_DIRS")
      gls_export_notfound("gls_${component}_DEFINITIONS")
      gls_export_notfound("gls_${component}_LIBRARIES")
      gls_export_notfound("gls_${component}_DEPENDENCIES")
    endif()
  endforeach()

  gls_export_variable(gls_INCLUDE_DIRS)
  gls_export_variable(gls_DEFINITIONS)
  gls_export_variable(gls_LIBRARIES)
endfunction()

#==============================================================================

# Use find_package_handle_standard_args to generate output. This handles the
# REQUIRED keyword, sets gls_FOUND, and generates the appropriate STATUS and
# FATAL_ERROR messages. We intentionally do not check gls_LIBRARIES here
# because it may be empty if the user did not specify any COMPONENTS.
include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(gls
  FOUND_VAR gls_FOUND
  REQUIRED_VARS gls_PACKAGE_INCLUDE_DIRS
  HANDLE_COMPONENTS)
